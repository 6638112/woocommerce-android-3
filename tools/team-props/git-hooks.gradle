static def isLinuxOrMacOs() {
    def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)
    println "osname = ${osName}"
    return osName.contains('linux') || osName.contains('mac os') || osName.contains('macos')
}

task copyGitHooks(type: Copy) {
    println 'Copying git-hooks from tools/team-props/git-hooks to the .git folder.'
    println "Root directory = ${rootDir}"
    description 'Copies the git hooks from tools/team-props/git-hooks to the .git folder.'
    from("${rootDir}/tools/team-props/git-hooks/") {
        include '**/*.sh'
        rename '(.*).sh', '$1'
    }
    into "${rootDir}/.git/hooks"
    onlyIf { isLinuxOrMacOs() }
}

task installGitHooks(type: Exec) {
    description 'Installs the pre-commit git hooks from tools/team-props/git-hooks.'
    println 'Installing the pre-commit git hooks from tools/team-props/git-hooks.'
    group 'git hooks'
    workingDir rootDir
    commandLine 'chmod'
    args '-R', '+x', '.git/hooks/'
    dependsOn copyGitHooks
    onlyIf { isLinuxOrMacOs() }
    doLast {
        logger.info('Git hook installed successfully.')
        println 'Git hook installed successfully.'
    }
}

afterEvaluate {
    // We install the hook at the first occasion
    tasks['clean'].dependsOn installGitHooks
    tasks['assemble'].dependsOn installGitHooks
}
