buildscript {
    ext.kotlinVersion = '1.2.20'

    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    apply plugin: 'checkstyle'

    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }

    task checkstyle(type: Checkstyle) {
        source 'src'
        include '**/*.java'
        include '**/*.kt'
        include '**/*.xml'
        exclude '**/gen/**'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            forcedModules = [
                    "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion",
                    "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            ]
        }
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.14.0'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    fluxCVersion = '599a767d6b664f5d3c0b5de073e469c9f39f080b'
    daggerVersion = '2.11'
    supportLibraryVersion = '26.1.0'
    glideVersion = '4.1.1'
    testRunnerVersion = '1.0.0'
    espressoVersion = '3.0.0'
}
