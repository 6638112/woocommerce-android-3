buildscript {
    ext.kotlinVersion = '1.2.31'

    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.automattic.android:fetchstyle:1.0'
    }
}

apply plugin: 'com.automattic.android.fetchstyle'

allprojects {
    apply plugin: 'checkstyle'

    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }

    task checkstyle(type: Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            forcedModules = [
                    "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion",
                    "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion",
                    "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            ]
        }
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.14.0'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    fluxCVersion = '875259736be890b94e30579e465d51e2cfb61926'
    daggerVersion = '2.11'
    supportLibraryVersion = '27.1.0'
    glideVersion = '4.1.1'
    testRunnerVersion = '1.0.0'
    espressoVersion = '3.0.0'
    constraintLayoutVersion = '1.0.2'
    multidexVersion = '1.0.2'
}
